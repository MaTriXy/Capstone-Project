apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

// Version information _ grabbed from {@url https://github.com/JakeWharton/u2020/blob/05a57bf43b9b61f16d32cbe8717af77cd608b0fb/build.gradle#L136-L140}
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 1 // Everytime a new branch is added we sum +1 to this

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.nunez.bookito"
        vectorDrawables.useSupportLibrary = true
        minSdkVersion 16
        targetSdkVersion 25
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner 'com.nunez.bookito.BookitoTestRunner'

    }
    buildTypes {
        release {
            minifyEnabled true // shrink
            useProguard true    // obfuscate
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false  // shrink
            useProguard false   // don't obfuscate

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        buildTypes.each {
            it.buildConfigField 'String', 'GoodreadsApiKey', project.GOODREADS_API_KEY
        }
    }
}

//configurations {
//    compile.exclude group: 'stax'
//    compile.exclude group: 'xpp3'
//}

def firebaseLibVersion = "10.2.1"

ext {
    supportVersion = "25.1.1"
    retrofitVersion = "2.2.0"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // support
    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile "com.android.support:design:${supportVersion}"
    compile "com.android.support:cardview-v7:${supportVersion}"

    // retrofit
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile("com.squareup.retrofit2:converter-simplexml:${retrofitVersion}") {
        exclude module: 'stax-api'
        exclude module: 'stax'
        exclude module: 'xpp3'
    }

    // image loading
    compile 'com.squareup.picasso:picasso:2.5.2'

    // firebase
    compile "com.google.firebase:firebase-auth:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-database:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-ads:$firebaseLibVersion"
    compile "com.google.firebase:firebase-core:$firebaseLibVersion"
    compile 'com.firebaseui:firebase-ui-auth:1.2.0'
    compile 'com.firebaseui:firebase-ui-database:1.2.0' //To use the Firebase RecyclerView Adapter

    // test
    androidTestCompile('com.github.andrzejchm.RESTMock:android:0.2.1', {
        exclude group: 'com.squareup.okhttp3'
    })
    androidTestCompile "com.android.support:support-annotations:${supportVersion}"
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.17'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
}

apply plugin: 'com.google.gms.google-services'